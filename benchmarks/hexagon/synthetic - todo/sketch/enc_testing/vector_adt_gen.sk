struct Buffer {
    int[ARRAY_LEN] data;
}

adt Vector {
    Ramp { Buffer buf; int base; int stride; int lanes; }
    Broadcast { int val; int lanes; }
    ADD { Vector v1; Vector v2; }
    MUL { Vector v1; Vector v2; }
    CONVX { Vector v; int kwidth; int[kwidth] kernel; }
}

// Constructors
Vector broadcast (int val, int lanes) {
    return new Broadcast(val=val, lanes=lanes);
}

Vector ramp (Buffer buffer, int base, int stride, int lanes) {
    return new Ramp(buf=buffer, base=base, stride=stride, lanes=lanes);
}

// Methods
int lanes (Vector v) {
    switch (v) {
        case Ramp: return v.lanes;
        case Broadcast: return v.lanes;
        case ADD: return lanes(v.v1);
        case MUL: return lanes(v.v1);
        case CONVX: return lanes(v.v);
    }
}


int get (Vector v, int i) {
    switch (v) {
        case Ramp: return v.buf.data[v.base + (v.stride * i)];
        case Broadcast: return v.val;
        case ADD: return get(v.v1, i) + get(v.v2, i);
        case MUL: return get(v.v1, i) * get(v.v2, i);
        case CONVX: {
            int r = 0;
            for (int k=0; k<v.kwidth && k<8; k++)
                r += get(v.v, k+i) * v.kernel[k];
            return r;
        }
    }
}

// Casting
Vector cast_uint8vec_to_int16vec (Vector vec) {
    return vec;
}

// Ops
Vector vec_add (Vector vec1, Vector vec2) {
    assert lanes(vec1) == lanes(vec2) : "Cannot add vectors of different size";
    
    return new ADD(v1=vec1, v2=vec2);
}

Vector vec_mul (Vector vec1, Vector vec2) {
    assert lanes(vec1) == lanes(vec2) : "Cannot multiply vectors of different size";
    
    return new MUL(v1=vec1, v2=vec2);
}

Vector convolve_x (Vector v, int kwidth, int[kwidth] kernel) {
    return new CONVX(v=v, kwidth=kwidth, kernel=kernel);
}

bit vec_eq (Vector vec1, Vector vec2) {
    if (lanes(vec1) != lanes(vec2))
        return false;

    // Only check for the first two values
    for (int i=0; i<2; i++)
        if (get(vec1, i) != get(vec2, i)) {
            return false;
        }
    return true;
}

// FIND TIME 364002 CHECK TIME 66998.2 TOTAL TIME 432000
// FIND TIME 118000 CHECK TIME 37000.5 TOTAL TIME 156000
